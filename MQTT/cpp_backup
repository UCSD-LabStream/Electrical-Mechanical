#include <WiFi.h>
#include <AsyncMqttClient.h>
#include <ArduinoJson.h>
#include <Stepper.h>
#include "esp_wpa2.h"

// WiFi credentials
// const char* ssid = "UCSD-PROTECTED";
const char* ssid = "nj";
// If using WPA enterprise
// const char* username = "yij036";
const char* password = "6265222296aA";


// MQTT broker config
//const char* mqtt_server = "broker.emqx.io";
const char* mqtt_server = "labstream.ucsd.edu";
const int   mqtt_port   = 9001;
const char* mqtt_path   = "/mqtt"; 
const char* mqtt_topic  = "stepper/labstream/motors/control";

// Stepper motors config
#define STEPS_PER_REV 2048

// Motor 1 Pins
#define M1_IN1 12
#define M1_IN2 13
#define M1_IN3 14
#define M1_IN4 15

// Motor 2 Pins
#define M2_IN1 16
#define M2_IN2 17
#define M2_IN3 18
#define M2_IN4 19

// Motor 3 Pins



// Global variables
Stepper stepper1(STEPS_PER_REV, M1_IN1, M1_IN3, M1_IN2, M1_IN4);
Stepper stepper2(STEPS_PER_REV, M2_IN1, M2_IN3, M2_IN2, M2_IN4);

int selectedMotor = 1;  // Default to motor 1
String currentCommand = "";
WiFiClient net;
AsyncMqttClient mqttClient;

void setup_wifi() {
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  /* If using WPA enterprise
  WiFi.mode(WIFI_STA);
  esp_wifi_sta_wpa2_ent_set_identity((uint8_t *)username, strlen(username));
  esp_wifi_sta_wpa2_ent_set_username((uint8_t *)username, strlen(username));
  esp_wifi_sta_wpa2_ent_set_password((uint8_t *)password, strlen(password));
  esp_wifi_sta_wpa2_ent_enable();

  WiFi.begin(ssid);
  */

  // If using WPA personal
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  int attempts = 0;
  while (WiFi.status() != WL_CONNECTED && attempts < 20) {
    delay(500);
    Serial.print(".");
    attempts++;
  }

  if (WiFi.status() == WL_CONNECTED) {
    Serial.println("");
    Serial.println("WiFi connected");
    Serial.print("IP address: ");
    Serial.println(WiFi.localIP());
  } else {
    Serial.println("Failed to connect to WiFi");
  }
}

void onMqttConnect(bool sessionPresent) {
  Serial.println("Connected to MQTT");
  Serial.print("Session present: ");
  Serial.println(sessionPresent);
  mqttClient.subscribe(mqtt_topic, 0);
  Serial.println("Subscribed to topic");
}

void onMqttDisconnect(AsyncMqttClientDisconnectReason reason) {
  Serial.println("Disconnected from MQTT");
  Serial.print("Reason: ");
  Serial.println(static_cast<int>(reason));
}

void onMqttMessage(char* topic, char* payload, AsyncMqttClientMessageProperties props,
  size_t len, size_t index, size_t total) {
  String msg;
  for (size_t i = 0; i < len; i++) msg += (char)payload[i];
    Serial.printf("Message arrived [%s]: %s\n", topic, msg.c_str());

  StaticJsonDocument<200> doc;
  DeserializationError error = deserializeJson(doc, msg);
  if (!error) {
    if (doc.containsKey("motor")) {
      selectedMotor = doc["motor"];
    }
    if (doc.containsKey("command")) {
      currentCommand = doc["command"].as<String>();
    }
  } else {
    if (msg == "cw" || msg == "ccw" || msg == "stop") {
      currentCommand = msg;
    }
  }
}

void testBrokerConnection() {
  WiFiClient testClient;
  Serial.print("Testing connection to ");
  Serial.print(mqtt_server);
  Serial.print(":");
  Serial.print(mqtt_port);
  Serial.println("...");
  
  if (testClient.connect(mqtt_server, mqtt_port)) {
    Serial.println("Successfully connected to broker port");
    testClient.stop();
  } else {
    Serial.println("Failed to connect to broker port");
    Serial.println("Possible causes:");
    Serial.println("- Wrong port number");
    Serial.println("- Firewall blocking connection");
    Serial.println("- Broker not running");
  }
}

void setup() {
  Serial.begin(115200);
  
  // Initialize stepper motor 1
  pinMode(M1_IN1, OUTPUT);
  pinMode(M1_IN2, OUTPUT);
  pinMode(M1_IN3, OUTPUT);
  pinMode(M1_IN4, OUTPUT);
  stepper1.setSpeed(10);

  // Motor 2
  pinMode(M2_IN1, OUTPUT);
  pinMode(M2_IN2, OUTPUT);
  pinMode(M2_IN3, OUTPUT);
  pinMode(M2_IN4, OUTPUT);
  stepper2.setSpeed(10);
  
  // Connect WiFi and MQTT
  setup_wifi();

  testBrokerConnection();
  
  // MQTT callbacks
  mqttClient.onConnect(onMqttConnect);
  mqttClient.onDisconnect(onMqttDisconnect);
  mqttClient.onMessage(onMqttMessage);
  
  // Set server and connect
  mqttClient.setServer(mqtt_server, mqtt_port);
  mqttClient.connect();
  
}

void loop() {
  
  Stepper* motor = (selectedMotor == 2) ? &stepper2 : &stepper1;

  if(currentCommand == "cw") {
    motor->step(1);
  } 
  else if(currentCommand == "ccw") {
    motor->step(-1);
  }
  else if(currentCommand == "stop") {
    digitalWrite(M1_IN1, LOW); digitalWrite(M1_IN2, LOW);
    digitalWrite(M1_IN3, LOW); digitalWrite(M1_IN4, LOW);
    digitalWrite(M2_IN1, LOW); digitalWrite(M2_IN2, LOW);
    digitalWrite(M2_IN3, LOW); digitalWrite(M2_IN4, LOW);
    currentCommand = "";
  }
  
  delay(3);
}